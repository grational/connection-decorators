/*
 * This build file was auto generated by 'gigawatt'
 * @author gsus
 * @date 17-11-2016 11.14
 */

// [plugins] this block must be the first
plugins {
	// support for groovy
	id 'groovy'
	id 'java-library'
	// autogenerate eclipse project files
	id 'eclipse'
	// create and html index linking every report generated
	id 'build-dashboard'
	// view of the project (jars used, navigable source tree)
	id 'project-report'
	// groovy code linter
	id 'codenarc'
	// test code coverage plugin
	id 'net.saliman.cobertura' version '4.0.0'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.12.1'
}

// [general] configuration
project.ext {
	groovyVersion     = '2.5.14'
	gradleVersion     = '6.8'
	spockVersion      = '1.3-groovy-2.5'
	cglibVersion      = '3.3.0'
	objenesisVersion  = '3.1'
	codenarcVersion   = '2.0.0'
	slf4jVersion      = '1.7.30'
	cacheDecVersion   = '0.3.0'
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group                 'it.italiaonline.rnd'
	version               scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)
	description           'Provide functionalities as caching and retrying to any URL connection.'
}

// target java 7
allprojects {
	sourceCompatibility = 8
	targetCompatibility = sourceCompatibility
}

repositories {
	jcenter()

	maven { url "https://jitpack.io" }
}

dependencies {
	implementation (
		"org.codehaus.groovy:groovy:$groovyVersion",
		"org.slf4j:slf4j-api:$slf4jVersion"
	)

	api (
		"com.github.grational:cache-decorators:$cacheDecVersion"
	)

	// We use the awesome Spock testing and specification framework
	testImplementation (
		"org.spockframework:spock-core:$spockVersion",
		"cglib:cglib-nodep:$cglibVersion",
		"org.objenesis:objenesis:$objenesisVersion"
	)
}

// codenarc groovy source analyses
codenarc{
	toolVersion = "$codenarcVersion"

	codenarcMain {
		configFile = rootProject.file('config/codenarc/ruleset.groovy')
		exclude "**/package-info.*"
		ignoreFailures true       // does not block builds
		maxPriority1Violations 0
		maxPriority2Violations 10
		maxPriority3Violations 20
	}
	codenarcTest {
		configFile = rootProject.file('config/codenarc/ruleset-test.groovy')
		ignoreFailures true
		maxPriority1Violations 0
		maxPriority2Violations 10
		maxPriority3Violations 20
	}
}

// [tasks] configuration //
jar {
	// execute check task when create the jar
	dependsOn 'check'
}

check {
	// clean before check
	dependsOn 'clean'
	// create the code coverage report
	dependsOn 'cobertura'
	// set the correct order for the dep tasks
	tasks.findByName('cobertura').mustRunAfter 'clean'
}

// [tasks] definition
task listJars {
	description = 'print out the jars used by the project'
	doLast {
		configurations.compile.each { File file -> println file.name }
	}
}

// vim: ft=groovy:fdm=indent
