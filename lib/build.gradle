/*
 * This build file was auto generated by 'gigawatt'
 * @author gsus
 * @date 17-11-2016 11.14
 */

// [plugins] this block must be the first
plugins {
	// support for groovy
	id 'groovy'
	// support for the api keyword in the dependencies section
	id 'java-library'
	// required by jitpack
	id 'maven-publish'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

// [general] configuration
project.ext {
	groovyVersion          = '3.0.9'
	spockVersion           = '2.0-groovy-3.0'
	slf4jVersion           = '1.7.32'
	cacheContainersVersion = 'release-1.1.1'
	urlDecoratorsVersion   = 'release-2.0.1'
	proxyEnvVersion        = 'release-1.2.3' 
	specSupportVersion     = 'release-0.1.6'
}

scmVersion {
	tag {
		prefix = 'release-'
	}
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group       'it.grational'
	version     scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)
	description 'Provide all major HTTP method implementations and additional functionalities as caching and retrying.'
}

allprojects {
	sourceCompatibility = 8
	targetCompatibility = sourceCompatibility
}

repositories {
	mavenCentral()

	maven { url "https://jitpack.io" }
}

dependencies {
	implementation (
		"org.slf4j:slf4j-api:$slf4jVersion"
	)

	api (
		"org.codehaus.groovy:groovy:$groovyVersion",
		"org.codehaus.groovy:groovy-json:$groovyVersion",
		"com.github.grational:cache-containers:$cacheContainersVersion",
		"com.github.grational:url-decorators:$urlDecoratorsVersion",
		"com.github.grational:proxy-environment:$proxyEnvVersion"
	)

	// We use the awesome Spock testing and specification framework
	testImplementation (
		"org.spockframework:spock-core:$spockVersion",
		"com.github.grational:specification-support:$specSupportVersion"
	)
}

// [tasks] configuration //
tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
}
jar {
	dependsOn 'check'
}

check {
	dependsOn 'clean'
}

// [tasks] definition
task uniTest(type: Test) {
	description = 'unit tests task'
	exclude '**/*ISpec*'
	exclude '**/*FSpec*'
}
task intTest(type: Test) {
	description = 'integration tests task'
	exclude '**/*USpec*'
	exclude '**/*FSpec*'
}
task funTest(type: Test) {
	description = 'functional tests task'
	exclude '**/*USpec*'
	exclude '**/*ISpec*'
}
gradle.startParameter.excludedTaskNames += "uniTest"
gradle.startParameter.excludedTaskNames += "intTest"
gradle.startParameter.excludedTaskNames += "funTest"

// required to work with jitpack.io
publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.grational'
			artifactId = 'connection-decorators'
			version = scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)

			from components.java
		}
	}
}

// vim: ft=groovy:fdm=indent
